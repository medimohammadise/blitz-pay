import org.springframework.boot.gradle.tasks.bundling.BootJar

plugins {
    alias(libs.plugins.skillsjars)
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.kotlin.spring)
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
    alias(libs.plugins.kotlin.jpa)
    alias(libs.plugins.cyclonedx)
    alias(libs.plugins.dependency.track)
}

group = "com.elegant.software.blitzpay"
version = "0.2.2"
description = "BlitzPay"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(providers.gradleProperty("javaVersion").get().toInt())
    }
}
tasks.named<BootJar>("bootJar") {
    archiveFileName.set("app.jar")
}


val mavenProxyUrl = providers.gradleProperty("mavenProxyUrl").orNull
val skillsJarsRepositoryUrl = providers.gradleProperty("skillsJarsRepositoryUrl").orNull
val sourceSets = the<org.gradle.api.tasks.SourceSetContainer>()
val contractTestSourceSet = sourceSets.create("contractTest") {
    java.setSrcDirs(listOf("src/contractTest/kotlin"))
    resources.setSrcDirs(listOf("src/contractTest/resources"))
    compileClasspath += sourceSets["main"].output + configurations["testCompileClasspath"]
    runtimeClasspath += output + compileClasspath + configurations["testRuntimeClasspath"]
}

repositories {
    mavenLocal()

    if (mavenProxyUrl != null) {
        maven { url = uri(mavenProxyUrl) }
    } else {
        mavenCentral()
    }

    maven { url = uri("https://repo.spring.io/snapshot") }

    if (skillsJarsRepositoryUrl != null) {
        maven { url = uri(skillsJarsRepositoryUrl) }
    }
}

dependencies {
    if (providers.gradleProperty("includeSkillsJars").map(String::toBoolean).orElse(false).get()) {
        // Optional: `com.skillsjars:jdubois__dr-jskill` runtime integration
        runtimeOnly(libs.dr.jskill)
    }
    // ----------------------------
    // Modulith: `invoice`
    // Purpose: ZUGFeRD/Factur-X invoice XML + Thymeleaf->PDF rendering
    // ----------------------------
    implementation(libs.mustang.library)
    implementation(libs.spring.boot.starter.thymeleaf)
    implementation(libs.flying.saucer.pdf)

    // ----------------------------
    // Modulith: `payments` (TrueLayer + QRPay)
    // Purpose: payment webhooks/signatures + SSE payment updates
    // ----------------------------
    implementation(libs.truelayer.java)
    implementation(libs.truelayer.signing) // TrueLayer official signing lib
    implementation(libs.nimbus.jose.jwt) // webhook signature verification
    implementation(libs.kotlin.logging.jvm) // Idiomatic kotlin logging (mu.KotlinLogging)
    implementation(libs.logstash.logback.encoder) // Structured JSON logging (production)
    implementation("io.projectreactor.kotlin:reactor-kotlin-extensions")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-reactor")

    // ----------------------------
    // Shared platform (all Modulith modules)
    // Purpose: web/http, JSON, Modulith runtime, OpenAPI
    // ----------------------------
    implementation(libs.spring.boot.starter.actuator)
    implementation(libs.spring.boot.starter.data.jpa)
    implementation(libs.spring.boot.starter.webflux)
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
    implementation("org.jetbrains.kotlin:kotlin-reflect")
    implementation("org.springframework.modulith:spring-modulith-starter-core")
    implementation("org.springframework.modulith:spring-modulith-starter-jpa")
    implementation(libs.springdoc.openapi.starter.webflux.ui)
    // TrueLayer Java SDK
    implementation(libs.truelayer.java)
    implementation(libs.truelayer.signing) // official signing lib
    implementation(libs.kotlin.logging.jvm) //Idiomatic kotlin logging
    implementation(libs.nimbus.jose.jwt) // Required for signature verification
    // Mustang Project – EU-standard ZUGFeRD / Factur-X invoice generation
    implementation(libs.mustang.library)
    // Thymeleaf templating engine for invoice PDF rendering
    implementation("org.springframework.boot:spring-boot-starter-thymeleaf")
    // Flying Saucer – converts Thymeleaf-rendered HTML to PDF
    implementation(libs.flying.saucer.pdf)

    // ----------------------------
    // Runtime support (used across modules)
    // ----------------------------
    runtimeOnly("org.postgresql:postgresql")
    runtimeOnly("org.springframework.modulith:spring-modulith-actuator")
    runtimeOnly("org.springframework.modulith:spring-modulith-observability")

    // ----------------------------
    // Tests (shared)
    // ----------------------------
    testImplementation(libs.spring.boot.starter.test)
    testImplementation(libs.spring.boot.starter.actuator.test)
    testImplementation(libs.spring.boot.starter.data.jpa.test)
    testImplementation(libs.spring.boot.starter.webflux.test)
    testImplementation(libs.spring.boot.testcontainers)
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit5")
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test")
    testImplementation("org.springframework.modulith:spring-modulith-starter-test")
    testImplementation("org.testcontainers:testcontainers-junit-jupiter")
    testImplementation("org.testcontainers:testcontainers-postgresql")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation(libs.mockito.kotlin)
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.modulith:spring-modulith-bom:${libs.versions.spring.modulith.get()}")
    }
}

configurations.named("contractTestImplementation") {
    extendsFrom(configurations.testImplementation.get())
}

configurations.named("contractTestRuntimeOnly") {
    extendsFrom(configurations.testRuntimeOnly.get())
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll("-Xjsr305=strict", "-Xannotation-default-target=param-property", "-jvm-target=25")
    }
}

allOpen {
    annotation("jakarta.persistence.Entity")
    annotation("jakarta.persistence.MappedSuperclass")
    annotation("jakarta.persistence.Embeddable")
}

tasks.withType<Test> {
    useJUnitPlatform()
}

tasks.register<Test>("contractTest") {
    description = "Runs Boot 4-compatible contract tests."
    group = "verification"
    testClassesDirs = contractTestSourceSet.output.classesDirs
    classpath = contractTestSourceSet.runtimeClasspath
    shouldRunAfter(tasks.named("test"))
    useJUnitPlatform()
}

tasks.named("check") {
    dependsOn("contractTest")
}



tasks.named<org.cyclonedx.gradle.CyclonedxAggregateTask>("cyclonedxBom") {
    jsonOutput.set(layout.buildDirectory.file("reports/cyclonedx/bom.json"))
}

dependencyTrackCompanion {
    url.set(providers.gradleProperty("dependencyTrackUrl").orElse(System.getenv("DT_URL") ?: ""))
    apiKey.set(providers.gradleProperty("dependencyTrackApiKey").orElse(System.getenv("DT_API_KEY") ?: ""))
    projectName.set(project.name)
    projectVersion.set(project.version.toString())
    autoCreate.set(true)
}
